if (ISBuildSdk.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 28

    defaultConfig {
        if (ISBuildSdk.toBoolean()) {
            applicationId "com.android.component.sdk"
        }
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (ISBuildSdk.toBoolean()) {
                manifest.srcFile 'src/main/runAlone/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    if (ISBuildSdk.toBoolean()) {
        implementation project(':comp-featureA')
        implementation project(':comp-featureB')
    } else if (IsBuildMudle.toBoolean()) {
        implementation project(':lib-base')
    } else {
        api project(':comp-featureA')
        api project(':comp-featureB')
    }

//    annotationProcessor 'com.alibaba:arouter-compiler:1.2.1'
}

task buildAar(type: Copy) {
    delete 'build/libs/comp-sdk.aar'
    from('build/outputs/aar/comp-sdk-release.aar')
    into('build/outputs/')
    include('comp-sdk-release.aar.jar')
    rename ('comp-sdk-release.aar.jar', 'comp-sdk.aar')
}

buildAar.dependsOn(build)

publish {
    userOrg = 'kylingo'
    groupId = 'com.github.kylingo'
    artifactId = 'component-sdk'
    publishVersion = '1.0.0'
    desc = 'Android component sdk library'
    website = 'https://github.com/kylingo/AndroidComponent'
}
